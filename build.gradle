plugins {
	id 'org.hidetake.ssh' version '2.9.0'
}

repositories {
    jcenter()
    maven {
    	url "http://192.168.255.11/maven2"
    }
}

remotes {
  webServer {
    host = '192.168.255.11'
    user = 'administrator'
    identity = file("${System.getProperty('user.home')}/.ssh/id_rsa")
  }
}

ssh.settings {
  timeoutSec = 10
  pty = true // for executeSudo() - password authentication only
  agent = true // ssh identity - executeSudo() does not work, but workarounds available
  fileTransfer = 'scp'
}

task deploy {
  doLast {
    ssh.run {
      session(remotes.webServer) {
      	def webappsDir = '/var/lib/tomcat8/webapps/'
      	executeSudo "chown -R administrator:administrator ${webappsDir} "
      	      	
      	def webProject = project(':module-web')
      	put from: webProject.war.archivePath, into: webappsDir
      	
      	executeSudo "chown -R tomcat8:tomcat8 ${webappsDir}"
      	executeSudo 'systemctl restart tomcat8'
      }
    }
  }
}